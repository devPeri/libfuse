# Compile helper programs
td = []
foreach prog: [ 'test_write_cache', 'test_setattr' ]
    td += executable(prog, prog + '.c',
                     include_directories: include_dirs,
                     link_with: [ libfuse ],
                     dependencies: thread_dep,
                     install: false)
endforeach
td += executable('test_syscalls', 'test_syscalls.c',
                 include_directories: include_dirs,
                 install: false)
td += executable('readdir_inode', 'readdir_inode.c',
                 include_directories: include_dirs,
                 install: false)

td += custom_target('test746.so',
                      input: 'test746.c',
                      output: 'test746.so',
                      build_by_default: true,
                      command: [
                        'gcc', '-W', '-Wall', '-fPIC',
                        '-Wl,-init,test746_init',
                        '-Wl,-z,initfirst',
                        '-shared',
                        '-o', '@OUTPUT@',
                        '@INPUT@',
                        '-ldl'
                      ])

test_scripts = [ 'conftest.py', 'pytest.ini', 'test_examples.py',
                 'util.py', 'test_ctests.py', 'test_custom_io.py',
                 'test746.py' ]
td += custom_target('test_scripts', input: test_scripts,
                      output: test_scripts, build_by_default: true,
                      command: ['cp', '-fPp',
                                '@INPUT@', meson.current_build_dir() ])

# Provide something helpful when running 'ninja test'

if meson.is_subproject()
	test('libfuse is a subproject, skipping tests', executable('wrong_command',
                      'wrong_command.c', install: false,
                       c_args: [ '-DMESON_IS_SUBPROJECT' ]))
else
	test('wrong_command', executable('wrong_command', 'wrong_command.c',
                      install: false))
endif
